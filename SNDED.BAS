REM SNDED
REM Sound Editor

USEGRAPHIC 5
USECLASS CRDINI

USEVAR LCDTURN
CRDINI::INIT()
IF CRDINI::ISSET("HORIZONTAL") THEN
  LCDTURN=0
ELSEIF CRDINI::ISSET("VERTICAL") THEN
  LCDTURN=270
ELSEIF CRDINI::ISSET("LCD0TURN") THEN
  LCDTURN=0
ELSEIF CRDINI::ISSET("LCD90TURN") THEN
  LCDTURN=90
ELSEIF CRDINI::ISSET("LCD180TURN") THEN
  LCDTURN=180
ELSEIF CRDINI::ISSET("LCD270TURN") THEN
  LCDTURN=270
ELSE
  LCDTURN=0
ENDIF

USEVAR X1,X2,Y1,Y2,XMIN,XMAX,YMIN,YMAX
XMIN=0X17A:XMAX=0XECB:YMIN=0X11A:YMAX=0XE81

USEVAR INITF,BGN,WMAX,HMAX,DAT,CNT,NUM

CLS
SPI 2500
INITF$="TOUCHPAD.INI"

IF FOPEN(INITF$,"R") THEN
  XMIN=VAL(FINPUT$())
  XMAX=VAL(FINPUT$())
  YMIN=VAL(FINPUT$())
  YMAX=VAL(FINPUT$())
  FCLOSE
ELSE
  GOSUB MINMAX
ENDIF

BGN=0
WMAX=SYSTEM(22)
HMAX=SYSTEM(23)
X=0
Y=0
DIM T(10)
DIM L(10)

DAT=DATAADDRESS(SND1)+6
CNT=0
LINE 0,HMAX-50,WMAX-1,HMAX-50,7

WHILE 1
  IF IN(0) THEN
    IF BGN=0 AND X!=0 AND Y!=0 THEN 
REM   PRINT "END"
      IF Y < HMAX-50 THEN
        IF CNT=0 THEN
         T(CNT)=X/10+1
          L(CNT)=(Y-50)*100
        ELSE
          IF X/10-T(CNT-1) <= 0 THEN CONTINUE
          T(CNT)=X/10-T(CNT-1)+1
          L(CNT)=(Y-50)*100
        ENDIF
        CIRCLEFILL X,Y,2,7
        NUM=CNT
        CNT=CNT+1
      ELSE
        FOR I=0 TO NUM
          DAT(I)=(T(I)<<16)+L(I)
        NEXT
        DAT(I)=1
        SOUND SND1
        CLS
        CURSOR 0,0
        FOR I=0 TO NUM+1
          PRINT "$";HEX$(DAT(I))
        NEXT
        CNT=0
        LINE 0,HMAX-50,WMAX-1,HMAX-50,7
      ENDIF
    ENDIF
    BGN=1
    CONTINUE
  ENDIF
  X1=GOSUB(TSCREAD,0X94)
  Y1=GOSUB(TSCREAD,0XD4)
  X2=GOSUB(TSCREAD,0X94)
  Y2=GOSUB(TSCREAD,0XD4)
  IF IN(0) THEN
    CONTINUE
  ENDIF
  IF ABS(X1-X2)>2 THEN CONTINUE
  IF ABS(Y1-Y2)>2 THEN CONTINUE
  X=(X1+X2)>>1
  Y=(Y1+Y2)>>1
  X=WMAX*(X-XMIN)/(XMAX-XMIN)-1
  Y=HMAX*(Y-YMIN)/(YMAX-YMIN)-1

  IF 0=LCDTURN THEN
    IF BGN=1 THEN
      BGN=0
    ENDIF
    REM 319-X,239-Y
    PRINT "Not supported."
    BREAK
  ELSEIF 90=LCDTURN THEN
    IF BGN=1 THEN
      BGN=0
    ENDIF
    REM 239-Y,X
    PRINT "Not supported."
    BREAK
  ELSEIF 180=LCDTURN THEN
    IF BGN=1 THEN
      BGN=0
REM     PRINT "START"
    ELSE
REM
    ENDIF
  ELSE
    IF BGN=1 THEN
      BGN=0
    ENDIF
    REM Y,319-X
    PRINT "Not supported."
    BREAK
  ENDIF
WEND
END

ALIGN4:EXEC 0
LABEL SND1
DATA 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,
     0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,
     0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,
     0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,
     0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0

LABEL TSCREAD
  REM I:COUNTER, D: DATA, R: RESULT, N:MINIMUM, X:MAXIMUM
  VAR I,D,R,N,X
  R=0
  N=0XFFFF
  X=0
  FOR I=1 TO 10
    D=ARGS(1)
    SPISWAPDATA &D,3
    D=((D AND 0X7F00)>>3) OR ((D AND 0XF80000)>>19)
    R=R+D
    IF D<N THEN N=D
    IF X<D THEN X=D
  NEXT
RETURN (R-N-X)>>3

LABEL MINMAX
  IF LCDTURN=0 OR LCDTURN=180 THEN
    LINE 0,0,30,30:LINE 0,0,10,0:LINE 0,0,0,10
    LINE 289,209,319,239:LINE 309,239,319,239:LINE 319,229,319,239
    CURSOR 0,15
    PRINT "DRAW LINES ON THE ALLOWS SEVERAL TIMES."
    PRINT "THEN, PRESS FIRE BUTTON."
  ELSE
    LINE 239,0,209,30:LINE 239,0,229,0:LINE 239,0,239,10
    LINE 0,319,30,289:LINE 0,319,10,319:LINE 0,319,0,309
    CURSOR 0,15
    PRINT "DRAW LINES ON THE ALLOWS "
    PRINT "SEVERAL TIMES."
    PRINT "THEN, PRESS FIRE BUTTON."
  ENDIF
  XMIN=0XFFFF:XMAX=0:YMIN=0XFFFF:YMAX=0
  DO
    IF KEYS(32) THEN BREAK
    IF IN(0) THEN CONTINUE
    X1=GOSUB(TSCREAD,0X94)
    Y1=GOSUB(TSCREAD,0XD4)
    X2=GOSUB(TSCREAD,0X94)
    Y2=GOSUB(TSCREAD,0XD4)
    IF IN(0) THEN CONTINUE
    IF ABS(X1-X2)>2 THEN CONTINUE
    IF ABS(Y1-Y2)>2 THEN CONTINUE
    X=(X1+X2)>>1
    Y=(Y1+Y2)>>1
    IF X<XMIN THEN XMIN=X
    IF XMAX<X THEN XMAX=X
    IF Y<YMIN THEN YMIN=Y
    IF YMAX<Y THEN YMAX=Y
    CURSOR 5,5:PRINT HEX$(XMIN),HEX$(XMAX),
    CURSOR 5,6:PRINT HEX$(YMIN),HEX$(YMAX),
  LOOP
  FOPEN(INITF$,"W")
  FPRINT XMIN
  FPRINT XMAX
  FPRINT YMIN
  FPRINT YMAX
  FCLOSE
RETURN
